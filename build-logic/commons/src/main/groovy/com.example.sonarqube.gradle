plugins {
    id('org.sonarqube')
}

// Add Project to https://sonarcloud.io/
// 1. Create organization
// 2. Go to Administration > Projects Management
// 3. Create Project[name=${project.name},key=${project.group}:${project.name}]
// 4. Go to Project > Administration > New Code > Number of days
sonarqube {
    properties {
        property 'sonar.projectKey', "${project.group}:${project.name}"
        property 'sonar.projectName', "${project.name}"
        property 'sonar.organization', 'srcmaxim'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.coverage.jacoco.xmlReportPaths', getJacocoReport()
        property 'sonar.java.checkstyle.reportPaths', getCheckstyleReport()
    }
}

private List<File> getJacocoReport() {
    getFromAggregation('build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml')
}

private List<File> getCheckstyleReport() {
    getFromAggregation('build/reports/checkstyle/linterReport.xml')
}

private List<File> getFromAggregation(def path) {
    def projectDir = rootProject.projectDir.toPath()
    def paths = projectDir
            .resolveSibling('aggregation/test-aggregate')
            .resolve(path)
            .toFile()
    List.of(paths)
}
